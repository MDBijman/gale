topdown[.f]: n -> .f > .map[.bottomup[.f]]; 
topdown[.f]: ?con(..sub) -> ?new_con(..new_sub) := .f in fin_sub := .topdown[.f] new_sub in ?new_con(..fin_sub);
topdown[.f]: (..ts) -> new_ts := .topdown[.f] ts in (..new_ts);
topdown[.f]: term -> .f term;

bottomup[.f]: ?con(..sub)    -> new_sub   := .map[.bottomup[.f]] sub in .f ?con(..new_sub);
bottomup[.f]: (..ts)         -> new_ts    := .map[.bottomup[.f]] ts  in .f (..new_ts);
bottomup[.f]: l -> .is_list l > new_elems := .map[.bottomup[.f]] l   in .f new_elems;
bottomup[.f]: t -> .f t;

env_bottomup[.f]: (e,  ?con(..sub))   -> (new_env, new_sub)   := .env_map[.env_bottomup[.f]] (e, sub) in .f (new_env, ?con(..new_sub));
env_bottomup[.f]: (e, (..ts))         -> (new_env, new_ts)    := .env_map[.env_bottomup[.f]] (e, ts)  in .f (new_env, (..new_ts));
env_bottomup[.f]: (e, l) -> .is_list l > (new_env, new_elems) := .env_map[.env_bottomup[.f]] (e, l)   in .f (new_env, new_elems);
env_bottomup[.f]: (e, t) -> .f (e, t); 

env_map[.f]: (env, list) -> .env_map_acc[.f] (env, list, []);
env_map_acc[.f]: (env, [], r) -> (env, .reverse r);
env_map_acc[.f]: (env, [n | t], r) ->
    (new_env, o) := .f (env, n)
  in .env_map_acc[.f] (new_env, t, [o | r]);
  
# Apply transformation on each term
map[.f]: [] -> [];
map[.f]: [h|t] -> .map_acc[.f] ([h|t], []);
map_acc[.f]: ([], acc)    -> acc > .reverse;
map_acc[.f]: ([h|t], acc) -> .map_acc[.f] (t, [.f h | acc]);

# Retain only items on which transformation succeeds
retain_all[.f]: [] -> [];
retain_all[.f]: [h|t] -> .f h > [h|.retain_all[.f] t] + .retain_all[.f] t;

try[.f]: n -> .f n + n;

# Reduce list with reduction function and start value
reduce[.f]: (end, []) -> end;
reduce[.f]: (start, [h|t]) -> .reduce[.f] (.f (start, h), t);

# Apply transformation on each term and then flatten
flatmap[.f]: e -> .is_list > .map[.f] e > .flatten; 
# Apply transformation on each term and then flatten some
flatmap_some[.f]: e -> .is_list > .map[.f] e > .flatten_some; 

# Puts a value between each element of a list as separator
intersperse[v]: [] -> [];
# Here we put 1 value in the accumulator to prevent a separator before the first element later
intersperse[v]: [h|t] -> .intersperse_acc[v] (t, [h]);

intersperse_acc[v]: ([], acc) -> .reverse acc;
intersperse_acc[v]: ([h|t], acc) -> .intersperse_acc[v] (t, [h|[v|acc]]);

# Flattens lists
# Empty list is flat
flatten_some: [] -> [];
# When head is list, concatenate with tail
flatten_some: [[h|t1]|t2] -> .concat ([h|t1], .flatten_some t2);
flatten_some: [[]|t] -> .flatten_some t;
# When head is not list, flatten tail
flatten_some: [h|t] -> [h|.flatten_some t];

# Flattens lists
# Empty list is flat
flatten: [] -> [];
# When head is list, concatenate with tail
flatten: [[h|t1]|t2] -> .concat ([h|t1], .flatten t2);

# Second element of pair
second: (_, b) -> b;
# Second element of pair
snd: (_, b) -> b;

# First element of pair
first:  (a, _) -> a;
# First element of pair
fst:  (a, _) -> a;

# Identity
id: e -> e;

# Fails if .f succeeds, succeeds if .f fails
not[.f]: e -> .f e > .fail + .id;

# Adds annotation a to term t
annotate[a]: t -> t {+a}; 

# Separate last elem of list
split_last: [h|t] -> .split_last_acc ([], [h|t]); 
split_last_acc: (acc, [h]) -> (.reverse acc, h);
split_last_acc: (acc, [h|t]) -> .split_last_acc ([h|acc], t);

# Length of list
len: l -> .is_list > .len_acc (l, 0);
len_acc: ([h|t], n) -> .len_acc (t, .add (n, 1));
len_acc: ([], n) -> n;

# Repeats a value n times in the form of a list
repeat[v]: n -> [];
repeat_acc[v]: (n, [h|t]) -> .gt (n, 0) > .repeat_acc[v] (.sub (n, 1), [v|[h|t]]) + [h|t];
repeat_acc[v]: (n, []) -> .repeat_acc[v] (.sub (n, 1), [v]);

# Given [a, b, c] and [d, e, f] give [a, d, b, e, c, f]
zip: ([h1|t1], [h2|t2]) -> .eq (.len t1, .len t2) > .zip_acc ([h1|t1], [h2|t2], []);
zip_acc: ([h1|t1], [h2|t2], acc) -> .zip_acc (t1, t2, [h1|[h2|acc]]);
zip_acc: ([], [], acc) -> acc;

# Base case: empty list
reverse: [] -> [];
# Dispatch to internal accumulator based impl. 
reverse: [h|t] -> .reverse_internal ([h|t], []);
# Base case: empty list
reverse_internal: ([],  acc)   -> acc;
# Recursive case: prepend head to accumulator and recurse
reverse_internal: ([h|t], acc) -> .reverse_internal (t, [h | acc]);

# One argument is an empty list
concat: ([], [h|t]) -> [h|t]; 
concat: ([h|t], []) -> [h|t]; 
# Both are non-empty lists
concat: ([h1|t1], [h2|t2]) -> .concat_acc (.reverse [h1|t1], .reverse [h2|t2], []);

concat_acc: ([h1|t1], [h2|t2], acc) -> .concat_acc ([h1|t1], t2, [h2|acc]); 
concat_acc: ([h1|t1], [], acc) -> .concat_acc (t1, [], [h1|acc]); 
concat_acc: ([], [], acc) -> acc; 

# Maps are just lists, so an empty map is an empty list
new_map: _ -> [];

# Base case: empty list
remove[needle]: [] -> [];
# Recursive case: check if head is needle, then return tail, otherwise recurse
remove[needle]: [(k, v)|t] -> .eq (needle, k) > t + [(k,v)|.remove[needle] t];

# Simple case: putting in empty list
put[key, value]: [] -> [(key, value)]; 
# Complex case: first remove key from map, then insert
put[key, value]: [h|t] -> 
  new_list := .remove[key] [h|t] in
    [(key, value)|new_list];

# Base case: empty list means not found
get[needle]: [] -> .fail;
# Recursive case: check if head is needle, then return value, otherwise recurse 
get[needle]: [(k,v)|t] -> .eq (needle, k) > v + .get[needle] t;

# Term variant checking rules 
is_list: [t|h] -> .id;
is_list: [] -> .id;
is_pair: (_, _) -> .id;
